plugins {
	id 'eu.geekplace.javapinning.common-conventions'
	id 'eu.geekplace.javapinning.publish-conventions'
	id 'com.android.library'
}

description = """\
Java Pinning's API for Android (min SDK level ${minAndroidSdk}"""

android {
    compileSdkVersion 30

    defaultConfig {
		versionName version
		// increase versionCode with every release
		// versionCode format:
		// <two-digits-major><two-digits-minor><two-digits-patch><two-digits-identifier>
		versionCode 1030100
		minSdkVersion minAndroidSdk
		namespace 'eu.geekplace.javapinning.android'
    }
    compileOptions {
        sourceCompatibility = javaVersion
		targetCompatibility project.sourceCompatibility
    }
	lintOptions {
		abortOnError true
	}

	publishing {
		singleVariant("release") {
			withSourcesJar()
			withJavadocJar()
		}
	}
}

dependencies {
	api project(':java-pinning-core')
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			afterEvaluate {
				from components.release
			}
		}
	}
}
